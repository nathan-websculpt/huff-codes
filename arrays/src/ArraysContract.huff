#define function pushToMyArray() nonpayable returns()
#define function popFromMyArray() nonpayable returns()
#define function getLengthOfMyArray() view returns (uint256)
#define function getValAtIndexOfMyArray(uint256) view returns (uint256)

// started from:
// https://github.com/Kuly14/Huff-Arrays/blob/main/src/DynamicArray.huff
#define constant ARRAY_LEN = FREE_STORAGE_POINTER()


#define macro GET() = takes(0) returns(0) {
    0x04 calldataload           // [index]
    [ARRAY_LEN]                 // [ARRAY_PTR, index]
    0x00 mstore                 // [index]
    0x20 0x00                   // [memOffset, size, index]
    sha3                        // [hash, index]

    [ARRAY_LEN] sload           // [Arr_size, hash, index]
    dup3                        // [index, Arr_size, hash, index]
    gt                          // [index > Arr_size, hash, index]
    outOfBounds
    jumpi                       // [hash, index]

    add                         // [hash + index]
    sload                       // [Result]
    0x20 mstore                 // []
    0x20 0x20                   // [memOffset, size]
    return                      // []

    outOfBounds:
        0x00 0x00 
        revert
}


#define macro POP() = takes(0) returns(0) {
    [ARRAY_LEN]                 // [ARRAY_PTR]
    0x00 mstore                 // []
    0x20 0x00                   // [memOffset, size]
    sha3                        // [hash]

    [ARRAY_LEN] sload           // [Array_length, hash]
    dup1                        // [Array_length, Array_length, hash]
    swap2                       // [Array_length, hash, Array_length]
    0x01                        // [0x01, Array_length, hash, Array_length]
    swap1                       // [Array_length, 0x01, hash, Array_length]
    sub                         // [Array_length - 1, hash, Array_length]

    add                         // [Last_item + hash, Array_length]

    0x00                        // [0, LastStorageSlot, Array_length]
    swap1                       // [LastStorageSlot, 0, Array_length]
    sstore                      // [Array_length]

    //update array length
    0x01 swap1 sub              // [0x01 - length]
    [ARRAY_LEN]                 // [ARRAY_PTR, newLength]
    sstore                      // []
    stop
}

#define macro PUSH() = takes(0) returns(0) {
    // store slot (ARRAY_LEN) pointer to 0th offset in memory
    [ARRAY_LEN]                 // [ARRAY_PTR]
    0x00                        // [0, ARRAY_PTR]
    mstore                      // []

    0x20 0x00                   // [memOffset, size]
    sha3                        // [hash]

    [ARRAY_LEN] sload           // [length, hash]

    dup1                        // [length, length, hash]
    swap2                       // [hash, length, length]
    add                         // [newStorageSlot, length]

    0x04 calldataload           // [value, newStorageSlot, length]
    swap1                       // [newStorageSlot, value, length]

    sstore                      // [length]

    //update array length
    0x01 add                    // [0x01 + length]
    [ARRAY_LEN]                 // [ARRAY_PTR, newLength]
    sstore                      // []
    stop
}


#define macro LENGTH() = takes(0) returns(0) {
    [ARRAY_LEN] sload           // [ARRAY_LENGTH]
    0x00 mstore                 // []

    0x20 0x00                   // [memOffset, Size]
    return                      // []
}


#define macro MAIN() = takes(0) returns(0) {
    0x00 calldataload 0xE0 shr

    dup1 __FUNC_SIG(pushToMyArray) eq push jumpi
    dup1 __FUNC_SIG(popFromMyArray) eq popLast jumpi
    dup1 __FUNC_SIG(getValAtIndexOfMyArray) eq get jumpi
    dup1 __FUNC_SIG(getLengthOfMyArray) eq length jumpi

    push:
        PUSH()
    popLast:
        POP()    
    get:
        GET()
    length:
        LENGTH()

    0x00 0x00
    revert
}

